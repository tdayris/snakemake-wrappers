default_config = {
    "fasta": "/path/to/fasta",
    "bed": "/path/to/bed",
    "msi_scan_extra": "",
    "msi_pro_extra": ""
}


try:
    if config == dict():
        config = default_config
except NameError:
    config = default_config


def get_best_scan() -> str:
    """Select cached indexes if available"""
    if config["msi_scan_extra"] == "-m 50":
        if config["fasta"] == "/mnt/beegfs/database/bioinfo/Index_DB/Fasta/Ensembl/GRCh38.99/GRCh38.99.homo_sapiens.dna.main_chr.fasta":
            return "msisensor/scan/msi.hg38.list"
        elif config["fasta"] == "/mnt/beegfs/database/bioinfo/Index_DB/Fasta/Ensembl/GRCh37.75/Homo_sapiens.GRCh37.75.dna.primary_assembly.fa":
            return "msisensor/scan/msi.hg19.list"
        elif config["fasta"] == "/mnt/beegfs/database/bioinfo/Index_DB/Fasta/Ensembl/GRCm38.99/GRCm38.99.mus_musculus.dna.fasta":
            return "msisensor/scan/msi.mm10.list"
    return "msisensor/scan/msi.list"

"""
Scan genome
Some rules are cached to speed up process
"""
rule msisensor_pro_scan:
    input:
        config["fasta"]
    output:
        temp("msisensor/scan/msi.list")
    message:
        "Gathring list of microsatellites from genome sequence"
    threads: 1
    resources:
        mem_mb=lambda wildcards, attempt: attempt * 1024,
        time_min=lambda wildcards, attempt: attempt * 45,
        tmpdir="tmp"
    log:
        "logs/msisensor/scan.log"
    params:
        extra =  config.get("msi_scan_extra", "")
    wrapper:
        "bio/msisensor_pro/scan"

rule msisensor_pro_scan_hg38:
    input:
        "/mnt/beegfs/database/bioinfo/Index_DB/Fasta/Ensembl/GRCh38.99/GRCh38.99.homo_sapiens.dna.main_chr.fasta"
    output:
        "msisensor/scan/msi.hg38.list"
    message:
        "Gathring list of microsatellites from genome HG38 sequence"
    threads: 1
    cache: True
    resources:
        mem_mb=lambda wildcards, attempt: attempt * 1024,
        time_min=lambda wildcards, attempt: attempt * 45,
        tmpdir="tmp"
    log:
        "logs/msisensor/scan.hg38.log"
    params:
        extra = ""
    wrapper:
        "bio/msisensor_pro/scan"

rule msisensor_pro_scan_hg19:
    input:
        "/mnt/beegfs/database/bioinfo/Index_DB/Fasta/Ensembl/GRCh37.75/Homo_sapiens.GRCh37.75.dna.primary_assembly.fa"
    output:
        "msisensor/scan/msi.hg19.list"
    message:
        "Gathring list of microsatellites from genome HG19 sequence"
    threads: 1
    cache: True
    resources:
        mem_mb=lambda wildcards, attempt: attempt * 1024,
        time_min=lambda wildcards, attempt: attempt * 45,
        tmpdir="tmp"
    log:
        "logs/msisensor/scan.hg19.log"
    params:
        extra =  ""
    wrapper:
        "bio/msisensor_pro/scan"

rule msisensor_pro_scan_mm10:
    input:
        "/mnt/beegfs/database/bioinfo/Index_DB/Fasta/Ensembl/GRCm38.99/GRCm38.99.mus_musculus.dna.fasta"
    output:
        "msisensor/scan/msi.mm10.list"
    message:
        "Gathring list of microsatellites from genome MM10 sequence"
    threads: 1
    cache: True
    resources:
        mem_mb=lambda wildcards, attempt: attempt * 1024,
        time_min=lambda wildcards, attempt: attempt * 45,
        tmpdir="tmp"
    log:
        "logs/msisensor/scan.mm10.log"
    params:
        extra =  ""
    wrapper:
        "bio/msisensor_pro/scan"

"""
Search for MSI
"""
rule msisensor_pro_msi:
    input:
        microsat=get_best_scan(),
        normal="sambamba/markdup/{sample}_normal.bam",
        normal_idx="sambamba/markdup/{sample}_normal.bam.bai",
        tumor="sambamba/markdup/{sample}_tumor.bam",
        tumor_idx="sambamba/markdup/{sample}_tumor.bam.bai",
        bed=config["bed"]
    output:
        multiext(
            "msisensor/{sample}/{sample}",
            ".msi",
            ".msi_dis",
            ".msi_germline",
            ".msi_somatic"
        )
    message: "Searching MSI in {wildcards.sample} from {input.microsat}"
    threads: 10
    resources:
        mem_mb=lambda wildcards, attempt: attempt * 1024 * 5,
        time_min=lambda wildcards, attempt: attempt * 35,
        tmpdir="tmp"
    log:
        "logs/msisensor_pro/msi/{sample}.log"
    params:
        extra = config.get("msi_pro_extra", "")
    wrapper:
        "bio/msisensor_pro/msi"
