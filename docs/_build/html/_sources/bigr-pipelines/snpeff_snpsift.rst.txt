.. _`SnpEff_SnpSift`:

SNPEFF_SNPSIFT
==============

Annotate VCF files with SnpEff and SNpSift

Usage
-----

In order to run the pipeline, use the following commands

.. code-block:: bash 

  # Go to your directory containing VCF files

  cd /path/to/VCF/dir

  # Copy/paste the following line for **HG38**

  bash /mnt/beegfs/pipelines/snakemake-wrappers/bigr_pipelines/snpeff_snpsift/run.sh hg38

  # Copy/paste the following line for **HG19**

  bash /mnt/beegfs/pipelines/snakemake-wrappers/bigr_pipelines/snpeff_snpsift/run.sh hg19


Input/Output
------------


**Input:**

 
  
* VCF files (searched in current working directory)
  
 


**Output:**

 
  
   
* ``snpsift``: Annotated VCF file
   
  
 





Used meta-wrappers
------------------

The following individual meta-wrappers are used in this pipeline:


* :ref:`meta/bio/snpeff_annotate`

* :ref:`meta/bio/snpsift`


Please refer to each meta-wrapper in above list for additional configuration parameters and information about the executed code.




Used wrappers
-------------

The following individual wrappers are used in this pipeline:


* :ref:`bio/tabix`

* :ref:`bio/compress/pbgzip`

* :ref:`bio/multiqc`


Please refer to each wrapper in above list for additional configuration parameters and information about the executed code.






Snakefile
---------

The pipeline contains the following steps:

.. code-block:: python

    import logging
    import os
    import pandas
    import sys
    from pathlib import Path

    logging.basicConfig(
        filename="snakemake.snpeff_snpsift.log",
        filemode="w",
        level=logging.DEBUG
    )

    worflow_source_dir = Path(next(iter(workflow.get_sources()))).absolute().parent
    common = str(worflow_source_dir / "../common/python")
    sys.path.append(common)

    from file_manager import *
    from files_linker import *
    from write_yaml import *
    from messages import *
    from snakemake.utils import min_version
    min_version("6.0")

    default_config = read_yaml(worflow_source_dir / "config.hg38.yaml")
    configfile: get_config(default_config)
    design = get_design(os.getcwd(), search_vcf_files)
    design["Sample_id"] = design["Sample_id"].str.replace("-", "_")

    container: "docker://continuumio/miniconda3:4.4.10"
    localrules: bigr_copy

    ruleorder: bigr_copy > pbgzip_compress

    samples_list = design["Sample_id"]

    wildcard_constraints:
        sample = r"|".join(samples_list)

    vcf_links = link_vcf(
        design.Sample_id,
        design.Upstream_file
    )

    last_vcf = (
        "bigr/cancer_gene_census/{sample}.vcf"
        if config["params"]["ncbi_build"] != "mm10"
        else "snpsift/dbsnp/{sample}.vcf"
    )



    ##################
    ### Flag rules ###
    ##################

    onsuccess:
        shell("touch DONE && rm --force --verbose ON_GOING ERROR")

    onerror:
        shell("touch ERROR && rm --force --verbose ON_GOING DONE")

    onstart:
        shell("touch ON_GOING && rm --force --verbose ERROR DONE")


    rule target_snpeff_snpsift:
        input:
            calls=expand(
                "snpsift/fixed/{sample}.vcf.gz",
                sample=samples_list,
                index=["", ".tbi"]
            ),
            qc="multiqc/SnpEff_annotation.html",
            tsv=expand(
                "snpsift/extractFields/{sample}.tsv",
                sample=samples_list
            ),
            description="columns_description.txt"
        output:
            directory("results_to_upload")
        message:
            "Finishing the annotation pipeline"
        params:
            ln = "--symbolic --force --relative --verbose",
            mkdir = "--parents --verbose",
            rs = "--checksum --verbose --recursive --human-readable"
        log:
            "logs/rsync/results.log"
        shell:
            "mkdir {params.mkdir} results_to_upload/ > {log} 2>&1 && "
            "rsync {params.rs} snpsift/fixed results_to_upload/VCF >> {log} 2>&1 && "
            "rsync {params.rs} snpsift/extractFields results_to_upload/TSV >> {log} 2>&1 && "
            "rsync {params.rs} multiqc results_to_upload/QC >> {log} 2>&1 && "
            "rsync {params.rs} columns_description.txt results_to_upload/ >> {log} 2>&1 "

    #################################
    ### FINAL VCF FILE INDEXATION ###
    #################################

    module compress_index_vcf_meta:
        snakefile: "../../meta/bio/compress_index_vcf/test/Snakefile"
        config: config


    use rule * from compress_index_vcf_meta


    # use rule pbgzip_compress from compress_index_vcf_meta with:
    #     output:
    #         protected("{tool}/{subcommand}/{sample}.vcf.gz")
    #
    #
    # use rule tabix_index from compress_index_vcf_meta with:
    #     output:
    #         protected("{tool}/{subcommand}/{sample}.vcf.gz.tbi")
    #     threads: 1


    #####################
    ### Export to TSV ###
    #####################

    rule extractfields:
        input:
            call="snpsift/fixed/{sample}.vcf.gz",
            call_index=get_tbi("snpsift/fixed/{sample}.vcf.gz")
        output:
            tsv="snpsift/extractFields/{sample}.tsv"
        message:
            "Making {wildcards.sample} annotated VCF readable"
        threads: 2
        resources:
            mem_mb=lambda wildcards, attempt: min(attempt * 4096, 15360),
            time_min=lambda wildcards, attempt: attempt * 20
        log:
            "logs/snpsift/extractAllFields/{sample}.log"
        params:
            extra="-s ';' -e '.'"
        wrapper:
            "bio/snpsift/extractAllFields"


    rule fix_vcf:
        input:
            vcf=last_vcf
        output:
            vcf=temp("snpsift/fixed/{sample}.vcf")
        message:
            "Removing empty fields, trailing ';' and non-canonical chromosomes "
            "for {wildcards.sample}"
        threads: 1
        resources:
            mem_mb=lambda wildcards, attempt: attempt * 1024,
            time_min=lambda wildcards, attempt: attempt * 15,
            tmpdir="tmp"
        log:
            "logs/bigr_scripts/fix_vcf/{sample}.log"
        params:
            default_chr=config["params"]["chr"],
            remove_non_conventional_chromosomes=True
        wrapper:
            "bio/BiGR/fix_vcf"


    ############################
    ### Column signification ###
    ############################


    rule column_signification:
        input:
            expand("snpsift/fixed/{sample}.vcf", sample=samples_list)
        output:
            "columns_description.txt"
        message:
            "Extract column description from VCF headers"
        threads: 8
        resources:
            mem_mb=lambda wildcards, attempt: attempt * 256,
            time_min=lambda wildcards, attempt: attempt * 15,
            tmpdir="tmp"
        log:
            "logs/columns_description.log"
        params:
        shell:
            "(cat {input} | grep -P '##INFO' | sort | uniq | "
            "grep -oP \"(ID=[^,]+|Description=.+$)\" | cut -f2- -d'=' | "
            "paste - - | sed 's/\\\">/\\\"/g') > {output} 2> {log}"

    ###############
    ### MultiQC ###
    ###############

    rule multiqc:
        input:
            expand(
                "snpeff/report/{sample}.html",
                sample=samples_list
            ),
            expand(
                "snpeff/csvstats/{sample}.csv",
                sample=samples_list
            )
        output:
            report(
                "multiqc/SnpEff_annotation.html",
                caption="../common/reports/multiqc.rst",
                category="Quality Controls"
            )
        message:
            "Aggregating quality reports from SnpEff"
        threads: 1
        resources:
            mem_mb=lambda wildcards, attempt: min(attempt * 1536, 10240),
            time_min=lambda wildcards, attempt: attempt * 35
        log:
            "logs/multiqc.log"
        wrapper:
            "bio/multiqc"


    ###################################
    ### OncoKB and CancerGeneCensus ###
    ### Custom annotations          ###
    ###################################


    rule cancer_gene_census_annotate:
        input:
            vcf="bigr/oncokb/{sample}.vcf",
            cgc=config["ref"]["cancer_census"]
        output:
            vcf=temp("bigr/cancer_gene_census/{sample}.vcf")
        message:
            "Adding CancerGeneCensus annotation in {wildcards.sample}"
        threads: 1
        resources:
            mem_mb=lambda wildcards, attempt: attempt * 1024 * 5,
            time_min=lambda wildcards, attempt: attempt * 25,
            tmpdir="tmp"
        log:
            "logs/bigr/cancer_gene_census_annotate/{sample}.log"
        wrapper:
            "bio/BiGR/cancer_gene_census_annotate"


    rule oncokb_annotate:
        input:
            #vcf="snpsift/clinvar/{sample}.vcf",
            vcf="bigr/format_to_info/{sample}.vcf",
            oncokb=config["ref"]["oncokb"]
        output:
            vcf=temp("bigr/oncokb/{sample}.vcf")
        message:
            "Adding OncoKB annotation in {wildcards.sample}"
        threads: 1
        resources:
            mem_mb=lambda wildcards, attempt: attempt * 1024 * 6,
            time_min=lambda wildcards, attempt: attempt * 35,
            tmpdir="tmp"
        log:
            "logs/bigr/oncokb/{sample}.log"
        wrapper:
            "bio/BiGR/oncokb_annotate"


    ####################
    ## Format to info ##
    ####################

    rule format_to_info:
        input:
            call="vcftools/mane/{sample}.vcf.gz"
        output:
            call="bigr/format_to_info/{sample}.vcf"
        message:
            "Annotating {wildcards.sample} with clear Format descriptions"
        threads: 1
        resources:
            mem_mb=lambda wildcards, attempt: attempt * 1024,
            time_min=lambda wildcards, attempt: attempt * 15,
            tmpdir="tmp"
        log:
            "logs/bigr/format_to_info/{sample}.log"
        params:
            extra = ""
        wrapper:
            "bio/BiGR/vcf_format_to_info"


    #####################
    ## MANE annotation ##
    #####################


    rule additional_headers_mane:
        output:
            temp("mane/description.txt")
        message:
            "Building annotation headers for MANE"
        threads: 1
        resources:
            mem_mb=lambda wildcards, attempt: attempt * 128,
            time_min=lambda wildcards, attempt: attempt * 2,
            tmpdir="tmp"
        log:
            "logs/mane/description.log"
        params:
            'key=INFO,ID=MANE_NCBI_GeneID,Number=1,Type=String,Description="NCBI_GeneID field from MANE"',
            'key=INFO,ID=MANE_Ensembl_Gene,Number=1,Type=String,Description="Ensembl_Gene field from MANE"',
            'key=INFO,ID=MANE_HGNC_ID,Number=1,Type=String,Description="HGNC_ID field from MANE"',
            'key=INFO,ID=MANE_symbol,Number=1,Type=String,Description="symbol field from MANE"',
            'key=INFO,ID=MANE_name,Number=1,Type=String,Description="name field from MANE"',
            'key=INFO,ID=MANE_RefSeq_nuc,Number=1,Type=String,Description="RefSeq_nuc field from MANE"',
            'key=INFO,ID=MANE_RefSeq_prot,Number=1,Type=String,Description="RefSeq_prot field from MANE"',
            'key=INFO,ID=MANE_Ensembl_nuc,Number=1,Type=String,Description="Ensembl_nuc field from MANE"',
            'key=INFO,ID=MANE_Ensembl_prot,Number=1,Type=String,Description="Ensembl_prot field from MANE"',
            'key=INFO,ID=MANE_MANE_status,Number=1,Type=String,Description="MANE_status field from MANE"',
            'key=INFO,ID=MANE_GRCh38_chr,Number=1,Type=String,Description="GRCh38_chr field from MANE"',
            'key=INFO,ID=MANE_chr_start,Number=1,Type=String,Description="chr_start field from MANE"',
            'key=INFO,ID=MANE_chr_end,Number=1,Type=String,Description="chr_end field from MANE"',
            'key=INFO,ID=MANE_chr_strand,Number=1,Type=String,Description="chr_strand field from MANE"',
        shell:
            'for PARAM in {params}; do echo "${{PARAM}}"; done > {output} 2> {log}'


    rule vcftools_annotate_mane:
        input:
            vcf = "vcftools/revel/{sample}.vcf.gz",
            annotation = config["ref"]["mane"],
            description = "mane/description.txt"
        output:
            vcf = temp("vcftools/mane/{sample}.vcf.gz")
        message:
            "Annotating {wildcards.sample} with MANE database, using VCFTools"
        threads: 4
        resources:
            mem_mb=lambda wildcards, attempt: attempt * 1024,
            time_min=lambda wildcards, attempt: attempt * 15,
            tmpdir="tmp"
        log:
            "logs/vcftools/annotate/{sample}.mane.log"
        params:
            extra = (
                "--columns INFO/MANE_NCBI_GeneID,INFO/MANE_Ensembl_Gene,"
                "INFO/MANE_HGNC_ID,INFO/MANE_symbol,INFO/MANE_name,"
                "INFO/MANE_RefSeq_nuc,INFO/MANE_RefSeq_prot,INFO/MANE_Ensembl_nuc,"
                "INFO/MANE_Ensembl_prot,INFO/MANE_MANE_status,CHROM,FROM,TO,"
                "INFO/MANE_chr_strand"
            )
        wrapper:
            "bio/vcftools/annotate"


    ######################
    ## Revel annotation ##
    ######################


    rule additional_headers_revel:
        output:
            temp("revel/description.txt")
        message:
            "Building annotation headers for REVEL"
        threads: 1
        resources:
            mem_mb=lambda wildcards, attempt: attempt * 128,
            time_min=lambda wildcards, attempt: attempt * 2,
            tmpdir="tmp"
        log:
            "logs/mane/description.log"
        params:
            'key=INFO,ID=REVEL_aaref,Number=1,Type=String,Description="Reference Amino Acid from REVEL"',
            'key=INFO,ID=REVEL_aaalt,Number=1,Type=String,Description="Alternative Amino Acid from REVEL"',
            'key=INFO,ID=REVEL,Number=1,Type=String,Description="REVEL score"',
            'key=INFO,ID=REVEL_Ensembl_transcriptid,Number=1,Type=String,Description="Ensemble transcript id from REVEL"',
        shell:
            'for PARAM in {params}; do echo "${{PARAM}}"; done > {output} 2> {log}'


    rule vcftools_annotate_revel:
        input:
            vcf = "vcftools/mistic/{sample}.vcf.gz",
            annotation = config["ref"]["revel"],
            description = "revel/description.txt"
        output:
            vcf = temp("vcftools/revel/{sample}.vcf.gz")
        message:
            "Annotating {wildcards.sample} with REVEL database, using VCFTools"
        threads: 4
        resources:
            mem_mb=lambda wildcards, attempt: attempt * 1024,
            time_min=lambda wildcards, attempt: attempt * 15,
            tmpdir="tmp"
        log:
            "logs/vcftools/annotate/{sample}.revel.log"
        params:
            extra = (
                "--columns CHROM,POS,-,REF,ALT,INFO/REVEL_aaref,INFO/REVEL_aaalt,INFO/REVEL,INFO/REVEL_Ensembl_transcriptid"
                if config["params"]["ncbi_build"] == "GRCh38" else
                "--columns CHROM,-,POS,REF,ALT,INFO/AAREF,INFO/AAALT,INFO/REVEL,INFO/REVEL_Ensembl_transcriptid"
            )
        wrapper:
            "bio/vcftools/annotate"


    #######################
    ## Mistic annotation ##
    #######################


    rule additional_headers_mistic:
        output:
            temp("mistic/description.txt")
        message:
            "Building annotation headers for REVEL"
        threads: 1
        resources:
            mem_mb=lambda wildcards, attempt: attempt * 128,
            time_min=lambda wildcards, attempt: attempt * 2,
            tmpdir="tmp"
        log:
            "logs/mane/description.log"
        params:
            'key=INFO,ID=MISTIC_score,Number=1,Type=String,Description="MISTIC high sensitivity prediction for exome analysis"',
            'key=INFO,ID=MISTIC_pred,Number=1,Type=String,Description="MISTIC prediction for global performance"',
        shell:
            'for PARAM in {params}; do echo "${{PARAM}}"; done > {output} 2> {log}'


    rule vcftools_annotate_mistic:
        input:
            vcf = "splice_ai/annot/{sample}.vcf.gz",
            annotation = config["ref"]["mistic"],
            description = "mistic/description.txt"
        output:
            vcf = temp("vcftools/mistic/{sample}.vcf.gz")
        message:
            "Annotating {wildcards.sample} with MISTIC database, using VCFTools"
        threads: 4
        resources:
            mem_mb=lambda wildcards, attempt: attempt * 1024,
            time_min=lambda wildcards, attempt: attempt * 15,
            tmpdir="tmp"
        log:
            "logs/vcftools/annotate/{sample}.mistic.log"
        params:
            extra = (
                "--columns CHROM,POS,REF,ALT,INFO/MISTIC_score,INFO/MISTIC_pred"
            )
        wrapper:
            "bio/vcftools/annotate"

    ########################################
    ## Compute occurence of each variant  ##
    ## within the complete cohort         ##
    ########################################


    rule variant_occurence_annotate:
        input:
            calls = ["snpsift/gnomad/{sample}.vcf"],
            occurence = "bigr/occurences/all_chroms.txt"
        output:
            calls = [temp("bigr/occurence_annotated/{sample}.vcf")]
        threads: 1
        resources:
            mem_mb=lambda wildcards, attempt: attempt * 1024,
            time_min=lambda wildcards, attempt: attempt * 15,
            tmpdir="tmp"
        log:
            "logs/variant_occurence/uncompress/{sample}.log"
        wrapper:
            "bio/variantoccurence/annotate"


    rule concatenate_per_chr_information:
        input:
            expand(
                "bigr/occurence/{chr}.txt",
                chr=config["params"]["chr"]
            )
        output:
            temp("bigr/occurences/all_chroms.txt")
        threads: 1
        resources:
            mem_mb=lambda wildcards, attempt: attempt * 1024,
            time_min=lambda wildcards, attempt: attempt * 15,
            tmpdir="tmp"
        log:
            "logs/variant_occurence/all.log"
        shell:
            "for i in {input}; do sed '1d' ${{i}}; done > {output} 2> {log}"


    rule variant_occurence_per_chr:
        input:
            calls=expand(
                "snpsift/vartype/{sample}.vcf",
                sample=samples_list
            )
        output:
            txt=temp("bigr/occurence/{chr}.txt")
        threads: 7
        resources:
            mem_mb=lambda wildcards, attempt: attempt * 1024,
            time_min=lambda wildcards, attempt: attempt * 45,
            tmpdir="tmp"
        log:
            "logs/variant_occurence/{chr}.log"
        wrapper:
            "bio/variantoccurence/chromosomes"



    ##########################
    ## Splice AI annotation ##
    ##########################


    rule splice_ai:
        input:
            vcf = "bigr/occurence_annotated/{sample}.vcf",
            fasta = config["ref"]["fasta"]
        output:
            vcf = temp("splice_ai/annot/{sample}.vcf.gz")
        message:
            "Adding Splice Variant annotation to {wildcards.sample}"
        threads: 10
        resources:
            mem_mb=lambda wildcards, attempt: attempt * 2048 * 8,
            time_min=lambda wildcards, attempt: attempt * 60 * 3,
            tmpdir="tmp",
            # gres="gpu:t4:1"
        params:
            annotation=config["params"].get("ncbi_build", "grch38").lower(),
            piped = True
        log:
            "logs/splice_ai/{sample}.log"
        wrapper:
            "bio/spliceai"


    #################################
    ### snpeff-snpsift annotation ###
    #################################

    snpeff_snpsift_config = {
        "ref": config["ref"],
        **config["snpeff_snpsift"]
    }

    module snpeff_meta:
        snakefile: "../../meta/bio/snpeff_annotate/test/Snakefile"
        config: snpeff_snpsift_config

    use rule snpeff from snpeff_meta with:
        input:
            calls="data_input/calls/{sample}.vcf.gz",
            calls_index="data_input/calls/{sample}.vcf.gz.tbi",
            db=config["ref"]["snpeff"]


    ##########################
    ### snpsift annotation ###
    ##########################


    module snpsift:
        snakefile: "../../meta/bio/snpsift/test/Snakefile"
        config: snpeff_snpsift_config

    use rule * from snpsift


    #################################################
    ### Gather files from iRODS or mounting point ###
    #################################################

    rule bigr_copy:
        output:
            "data_input/calls/{sample}.vcf.gz"
        message:
            "Getting {wildcards.sample} VCF file"
        threads: 1
        resources:
          mem_mb=lambda wildcards, attempt: min(attempt * 1024, 2048),
          time_min=lambda wildcards, attempt: attempt * 45,
        params:
            input=lambda wildcards, output: vcf_links[output[0]]
        log:
            "logs/bigr_copy/{sample}.log"
        wrapper:
            "bio/BiGR/copy"




Authors
-------


* Thibault Dayris
